# zsh completion for codex-use

if ! type compinit >/dev/null 2>&1; then
  autoload -Uz compinit
  compinit
fi

: ${CODEX_HOME:="$HOME/.codex"}
local _cx_envdir="${CODEX_HOME}/envs"

_cx_list_envs() {
  local -a envs raw
  raw=(${_cx_envdir}/*.env(N:t:r))
  local name
  for name in ${raw[@]:-}; do
    if [[ ${name:l} == *chatgpt* ]]; then
      continue
    fi
    envs+=("$name")
  done
  print -l -- $envs
}

_codex-use() {
  local -a subs
  subs=(
    'list:列出所有环境'
    'new:新建环境'
    'edit:编辑环境'
    'del:删除环境'
    'show:显示已记忆与当前变量'
    'current:同 show'
    'help:显示帮助'
    'open:打开环境目录'
    'dir:打开环境目录（同 open）'
    'chatgpt:切换到 ChatGPT 浏览器模式'
  )

  local -a envs
  envs=($(_cx_list_envs))

  if (( CURRENT == 2 )); then
    _describe -t subcmds '子命令' subs
    compadd -X '环境（来自 envs 目录）' -a envs
    return
  fi

  if (( CURRENT == 3 )); then
    case ${words[2]} in
      edit|del|delete|rm)
        compadd -a envs
        return
        ;;
      new)
        return
        ;;
    esac
  fi
}

compdef _codex-use codex-use
